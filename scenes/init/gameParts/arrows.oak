std := import('std')

fn addArrowBar(p?, x, y, inv, extra...) {
  comps := [
    pos(x, y)
  ].concat(extra)

  bar := if p? {
    ? -> add(comps)
    _ -> p?.add(comps)
  }

  arrows := []
  wh := 40
  padRatio := 4

  macro := if inv {
    m! := [
      [:left, rgb(11, 177, 96)]
      [:up, rgb(255, 94, 61)]
      [:down, rgb(162, 59, 188)]
      [:right, rgb(213, 61, 66)]
    ]

    1 -> m!
    -1 -> m!.reverse()
  }

  fn getPad() wh / padRatio
  fn getP() arrows.length * (wh + getPad())
  fn getTotalW() ((wh / 2) + getP()) * inv
  fn sizeOf() getP() + wh

  fn tryForeign(c, args) try(fn() c(args))

  fn makeArrow() {
    arrow := with bar.add() [
      'Arrow' |> s()

      area()
      anchor('center' |> s())
      with rect(wh, wh) { fill: false }

      pos(getTotalW(), wh / 2)
      {
        self: arrow
        macroIndex: macroIndex := arrows.length

        // Turn this off if you like arrows in any lane!
        NEEDS_MACRO_INDEX: true

        noteTrack: []

        ON_STATE: ON_STATE := :on
        OFF_STATE: OFF_STATE := :off
        state: OFF_STATE

        defColor: WHITE
        pshColor: macro.(macroIndex).1

        idleEvents: []
        clickEvents: []
        holdEvents: []
        holdWinEvents: []
        enterEvents: []
        leaveEvents: []

        STATE_CHANGE_TIME: 0
        updateSCT: fn() arrow.STATE_CHANGE_TIME <- time()

        press: fn {
          arrow.state <- ON_STATE
          arrow.updateSCT()
        }

        unpress: fn {
          arrow.state <- OFF_STATE
          arrow.updateSCT()
        }

        getState: fn() arrow.state
      }
    ]

    arrow.arrowSpr := with arrow.add() [
      { macroIndex: macroIndex := arrow.macroIndex }

      with sprite('arrows' |> s()) {
      }

      color(WHITE)

      anchor('center' |> s())
      scale(1.5)

      with rotate() if macro.(macroIndex).0 {
        :up -> 0
        :left -> -90
        :down -> 180
        :right -> 90
      }
    ]

    arrow.updateColor := fn if arrow.getState() {
      fn useCol(c) with arrow.arrowSpr.use() color(c)
      :off -> useCol(arrow.defColor)
      :on -> useCol(arrow.pshColor)
    }

    fn tryEventStack(es, args) with es.forEach() fn(c) tryForeign(c, args)

    with arrow.onCollide('Note' |> s()) fn(n) {
      tryEventStack(arrow.enterEvents, n)
    }

    with arrow.onCollideEnd('Note' |> s()) fn(n) {
      tryEventStack(arrow.leaveEvents, n)
    }

    with arrow.onCollideUpdate('Note' |> s()) fn(n) {
      if n.isColliding(arrow) -> if arrow.state {
        fn doCollide(p) tryEventStack(p, n)

        // Handle Holding
        n.timeHeld <- n.timeHeld + dt()
        tryEventStack(arrow.holdEvents, n)
        if n.holdLength < n.timeHeld -> tryEventStack(arrow.holdWinEvents, n)

        :on -> doCollide(arrow.clickEvents)
        :off -> doCollide(arrow.idleEvents)
      } 
    }

    arrow.makeNote := fn(offset, holdLength) {
      isHoldNote := (holdLength != ?) & (holdLength > 1)

      note := with arrow.add() [
        'Note' |> s()

        area()
        anchor('center' |> s())
        with rect(wh, wh) { fill: false }

        pos(0, offset)
        {
          macroIndex: arrow.macroIndex
          isHoldNote: isHoldNote
          holdLength: holdLength
          timeHeld: 0

          Pressed?: false
          Missed?: false
        }
      ]

      note.noteSpr := with note.add() [
        { macroIndex: macroIndex := note.macroIndex }

        with sprite('arrows' |> s()) {
        }

        color(macro.(macroIndex).1)

        anchor('center' |> s())
        scale(1.5)

        with rotate() if macro.(macroIndex).0 {
          :up -> 0
          :left -> -90
          :down -> 180
          :right -> 90
        }
      ]

      arrow.noteTrack.push(note)
      note
    }

    arrow
  }

  fn stepColors with arrows.forEach() fn(a) {
    a.updateColor()
  }

  fn stepArrows(TRACK_SPEED) with arrows.forEach() fn(a) {
    with a.noteTrack.forEach() fn(nt) {
      nt.move(0, TRACK_SPEED)
    }
  }

  fn addArrow() with arrows.push() makeArrow()
  fn addArrows(r) with std.loop(r) fn() addArrow()

  addArrows(4)

  {
    bar: bar

    wh: wh
    sizeOf: sizeOf

    makeArrow: makeArrow
    addArrow: addArrow

    arrows: arrowsCB! := fn() arrows
    withAllArrows: fn(cb) arrowsCB!().forEach(cb)

    stepArrows: stepArrows
    stepColors: stepColors
    trackNote!: fn(i, o) arrowsCB!().(i).makeNote(o)

    padRatio: padRatio
    setPadRatio: fn(a) padRatio <- a
  }
}